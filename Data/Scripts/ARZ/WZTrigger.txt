// ----------------------------------
// RSDK Project: Sonic 2
// Script Description: WZ Trigger Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

// ========================
// Aliases
// ========================

private alias object.value0 : object.timer

private alias 0 : WZTRIGGER_BLANK
private alias 1 : WZTRIGGER_PAUSE
private alias 2 : WZTRIGGER_FADEOUT

// Player Aliases
private alias object.state 		: player.state
private alias object.yvel 		: player.yvel
private alias object.animation 	: player.animation
private alias object.interaction : player.interaction
private alias object.value25 	: player.gravityStrength

// Water Aliases
private alias object.value3 : object.proccessActEffects

// Air Bubbler Aliases
private alias object.value5 : object.forceActive

// Air Bubble Aliases
private alias object.value1 : object.originPos.x

// Game Modes
private alias 2 : MODE_TIMEATTACK


// ========================
// Events
// ========================

event ObjectUpdate
	// Check if the camera bounds should change now
	BoxCollisionTest(C_TOUCH, object.entityPos, -32, 0, 32, 5120, 0, 0, 0, 0, 0)
	if checkResult == true
		stage.deathBoundary = 0x7FFFFFFF
	end if

	foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
		// Check player collision
		BoxCollisionTest(C_TOUCH, object.entityPos, -32, 0, 32, 256, currentPlayer, 0, 0, 0, 0)
		if checkResult == true
			// Player's touched this object, let's start the transition!

			// Let's make sure the player's not climbing or anything
			player[currentPlayer].state = Player_State_Air
			player[currentPlayer].animation = ANI_FANROTATE
			player[currentPlayer].gravityStrength = 0x2000 // let's make the player fall a bit faster..
			player[currentPlayer].interaction = false // the bubbles are a neat effect but we better not interact with them lol

			// If P1, switch to main state
			if currentPlayer == 0
				if object.state == WZTRIGGER_BLANK
					object.state = WZTRIGGER_PAUSE
					
					foreach (TypeName[Water], arrayPos0, ACTIVE_ENTITIES)
						object[arrayPos0].proccessActEffects = Player_State_Static // reset the object's water effects function so that we can set stage.newWaterLevel ourselves
						stage.newWaterLevel = 0x7FFF0000 // let's pull the water down..
					next
				end if
			end if
		end if
	next

	switch object.state
	case WZTRIGGER_PAUSE
		// Letting the player fall for a second
		object.timer++
		if object.timer == 80
			object.timer = 0
			object.state++
		end if
		break

	case WZTRIGGER_FADEOUT
		// Fading out to go to WZ
		music.volume -= 4
		object.timer += 6
		SetScreenFade(0, 0, 0, object.timer)
		if object.timer == 384
			starPostID = 0
			stage.listPos = 22
			StopMusic()
			LoadStage()
		end if
		break
		
	end switch
end event


event ObjectStartup
	// Cycle through all WZ Trigger objects and...
	foreach (TypeName[WZ Trigger], arrayPos0, ALL_ENTITIES)
		CheckEqual(options.gameMode, MODE_TIMEATTACK)
		checkResult |= options.vsMode
		if options.gameMode == MODE_TIMEATTACK
			// If in VS or time attack, just make this a normal pit
			ResetObjectEntity(arrayPos0, TypeName[Blank Object], 0, 0, 0)
		else
			object[arrayPos0].priority = PRIORITY_XBOUNDS
			
			// Setup for all the following bubblers
			arrayPos1 = arrayPos0
			arrayPos1++
			while object[arrayPos1].type == TypeName[Air Bubbler]
				object[arrayPos1].drawOrder = 8 // (make it invis)
				object[arrayPos1].priority = PRIORITY_XBOUNDS
				object[arrayPos1].forceActive = true // (this var is new)
				
				arrayPos1++
				if arrayPos1 >= 0x420
					arrayPos1 = 0
				end if
			loop
			
			// And now, it's the bubbles' turn!
			while object[arrayPos1].type == TypeName[Air Bubble]
				object[arrayPos1].originPos.x = object[arrayPos1].xpos
				object[arrayPos1].frame = object[arrayPos1].propertyValue
				
				arrayPos1++
				if arrayPos1 >= 0x420
					arrayPos1 = 0
				end if
			loop
		end if
	next
end event


// ========================
// Editor Events
// ========================

event RSDKDraw
	DrawSprite(0)
	
	if editor.showGizmos == true
		editor.drawingOverlay = true
		
		// Draw this object's giant hitbox, a lone "T" icon doesn't do this object justice
		
		temp0 = 68; temp1 = -512; temp2 = 68; temp3 = 5120;
		CallFunction(EditorHelpers_DrawHitbox)
		
		editor.drawingOverlay = false
	end if
end event


event RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-8, -8, 16, 16, 168, 18) // "trigger" - #0

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end event
