// ----------------------------------
// RSDK Project: Sonic 2
// Script Description: SSZ Trigger Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

// ========================
// Aliases
// ========================

private alias object.value0 : object.timer

private alias 0 : SSZTRIGGER_BLANK
private alias 1 : SSZTRIGGER_PAUSE
private alias 2 : SSZTRIGGER_FADEOUT

// Player Aliases
private alias object.state 		: player.state
private alias object.yvel 		: player.yvel
private alias object.animation 	: player.animation
private alias object.value0 	: player.rings
private alias object.value25 	: player.gravityStrength

// Game Modes
private alias 2 : MODE_TIMEATTACK


// ========================
// Events
// ========================

event ObjectUpdate
	// Check if the camera bounds should change now
	BoxCollisionTest(C_TOUCH, object.entityPos, -68, 0, 68, 5120, 0, 0, 0, 0, 0)
	if checkResult == true
		stage.curYBoundary1 = -0x1000
	end if

	foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
		BoxCollisionTest(C_TOUCH, object.entityPos, -68, 0, 68, 256, currentPlayer, 0, 0, 0, 0)
		temp0 = checkResult
		CheckEqual(player[currentPlayer].state, Player_State_Static) // Let's make sure that the player was launched by a cannon, we don't want Tails's flying to be able to trigger it!
		temp0 &= checkResult
		if temp0 == true
			// Player has touched this object, start the transition

			// Player should already be in this state, but it doesn't hurt
			// player[currentPlayer].state = Player_State_Air

			// Enforce maximum Y velocity
			// if player[currentPlayer].yvel >= 0x100000
			// 	player[currentPlayer].yvel = 0x100000
			// 	player[currentPlayer].yvel -= player[currentPlayer].gravityStrength
			// end if

			// If P1, switch to main state
			if currentPlayer == 0
				if object.state == SSZTRIGGER_BLANK
					object.state = SSZTRIGGER_PAUSE
				end if
			end if
		end if
	next

	switch object.state
	case SSZTRIGGER_PAUSE
		// Letting the player fall for a second
		object.timer++
		if object.timer == 40
			object.timer = 0
			object.state++
		end if
		break

	case SSZTRIGGER_FADEOUT
		// Fading out to go to SSZ
		music.volume -= 4
		object.timer += 24
		SetScreenFade(0, 0, 0, object.timer)
		if object.timer == 384
			starPostID = 0
			stage.listPos = 24
			StopMusic()
			LoadStage()
		end if
		break
		
	end switch
end event


event ObjectStartup
	// Cycle through all SSZ Trigger objects and...
	foreach (TypeName[SSZ Trigger], arrayPos0, ALL_ENTITIES)
		if options.gameMode == MODE_TIMEATTACK
			// Erase it if in time attack
			ResetObjectEntity(arrayPos0, TypeName[Blank Object], 0, 0, 0)
		else
			if options.vsMode == false
				// Give it the XBOUNDS priority if in single player
				object[arrayPos0].priority = PRIORITY_XBOUNDS
			else
				// Erase it if in 2P mode
				ResetObjectEntity(arrayPos0, TypeName[Blank Object], 0, 0, 0)
			end if
		end if
	next
end event


// ========================
// Editor Events
// ========================

event RSDKDraw
	DrawSprite(0)
	
	if editor.showGizmos == true
		editor.drawingOverlay = true
		
		// Draw this object's giant hitbox, a lone "T" icon doesn't do this object justice
		
		temp0 = 68; temp1 = -512; temp2 = 68; temp3 = 5120;
		CallFunction(EditorHelpers_DrawHitbox)
		
		editor.drawingOverlay = false
	end if
end event


event RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-8, -8, 16, 16, 168, 18) // "trigger" - #0

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end event
